load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_run_binary")
load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("//:build/wd_ts_project.bzl", "wd_ts_project")
load("//:build/wd_ts_test.bzl", "wd_ts_test")
load("//:build/wd_ts_type_test.bzl", "wd_ts_type_test")

wd_ts_project(
    name = "types_lib",
    srcs = glob(
        ["src/**/*"],
        exclude = ["src/worker/**/*"],
    ),
    deps = [
        "//:node_modules/@types",
        "//:node_modules/@workerd/jsg",
        "//:node_modules/capnp-ts",
        "//:node_modules/typescript",
    ],
)

js_binary(
    name = "types_cli_bin",
    data = [":types_lib"],
    entry_point = "src/cli/index.js",
    node_options = ["--enable-source-maps"],
)

js_run_binary(
    name = "types_internal_d_ts",
    srcs = [
        "//src/workerd/tools:api_encoder",
    ] + glob(["defines/**/*.ts"]),
    outs = [
        "comments.json",
        "index.d.ts",
    ],
    args = [
        "--rtti-capnp-path",
        "src/workerd/tools/api.capnp.bin",
        "--defines-path",
        "types/defines",
        "--comments-output-path",
        "types/comments.json",
        "--output-path",
        "types/index.d.ts",
    ],
    silent_on_success = False,  # Always enable logging for debugging
    tool = ":types_cli_bin",
)

npm_package(
    name = "types_internal",
    srcs = [":types_internal_d_ts"],
    # Required to ensure source files are copied when running internal builds
    # that depend on `workerd` as an external repository
    include_external_repositories = ["workerd"],
    visibility = ["//visibility:public"],
)

js_binary(
    name = "build_worker_bin",
    entry_point = "scripts/build-worker.mjs",
)

js_run_binary(
    name = "types_worker",
    srcs = [
        ":types_internal",
        "//:node_modules/esbuild",
        "//:node_modules/@workerd/jsg",
        "//:node_modules/capnp-ts",
        "//:node_modules/typescript",
        "//src/workerd/tools:param_extractor",
    ] + glob(
        [
            "src/**/*.ts",
            "src/**/*.mjs",
            "defines/**/*.ts",
        ],
        exclude = ["src/cli/**/*"],
    ),
    outs = ["dist/index.mjs"],
    tool = ":build_worker_bin",
)

js_binary(
    name = "build_types_bin",
    entry_point = "scripts/build-types.mjs",
)

js_run_binary(
    name = "types",
    srcs = [
        "scripts/config.capnp",
        ":types_worker",
        "//src/workerd/server:workerd",
    ],
    out_dirs = ["definitions"],
    silent_on_success = False,  # Always enable logging for debugging
    tool = ":build_types_bin",
)

[
    wd_ts_test(
        src = src,
        node_options = ["--enable-source-maps"],
        deps = [":types_lib"],
    )
    for src in glob(["test/**/*.spec.ts"])
]

[
    wd_ts_type_test(src = src)
    for src in glob(["test/types/**/*.ts"])
]
